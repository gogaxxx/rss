#!/usr/bin/perl
#
# detect an RSS or Atom feed on web page
#

use strict;
use warnings;

use Agg::Config;
use Agg::Download;
use HTML::Parser;

MAIN: {
	my $url = $ARGV[0];

	unless (defined($url) && $url ne '') {
		die("Specify an URL\n");
	}

	my $cfg = Agg::Config->get_global_config();
	my $loader = Agg::Download->new($cfg);

	my $content = $loader->fetch_recent($url);

	print "$url :\n";

	my $parser = HTML::Parser->new(
		start_h => [ \&start_tag, "self,tagname,attr"]
	);
	$parser->{'rss_detected'} = 0;
	$parser->{'base_url'} = url_domain($url);

	$parser->parse($content);

	unless ($parser->{'rss_detected'}) {
		print "\tNo feeds on this page\n";
	}
	print "\n";
}

sub start_tag {
	my ($parser, $tag_name, $tag_attrs)=@_;

	$tag_name = lc($tag_name);
	if ($tag_name eq 'link'
		&& $tag_attrs->{'rel'} eq 'alternate') 
	{
		unless ($parser->{'rss_detected'}) {
			print "\tURL\t\tType\n";
			print "\t-----------------------------------\n";
		}
		print "\t", full_url($tag_attrs->{'href'}, $parser->{'base_url'}),
				"\t\t", $tag_attrs->{'type'}, "\n";

		$parser->{'rss_detected'} = 1;
	}
}

sub url_domain {
	my $url = shift;

	if ($url =~ m{^(http[s]?://[^/]+)}o) {
		return $1.'/';
	}
}

sub full_url {
	my ($url, $base_url)=@_;

	$url =~ m{^http[s]?://}
		? $url
		: $base_url.$url;
}
